use lib "./inc";
our %PREREQ_PM;
BEGIN {
   %PREREQ_PM= (
      #'ExtUtils::Depends' => '0.405',
      'ExtUtils::CBuilder' => '0.2800',
      'Crypt::SecretBuffer' => '0.018',
   );
   # If the prereqs for this script are missing,
   # write out a Makefile that tells CPAN to install them
   my $use_prereqs_code= join('', map { "use $_ $PREREQ_PM{$_}; " } keys %PREREQ_PM).'1;';
   print $use_prereqs_code."\n";
   unless (eval $use_prereqs_code) {
      warn "$@\n";
      WriteMakefile(
         NAME         => 'Crypt::MultiKey',
         PREREQ_FATAL => 1,
         PREREQ_PM    => \%PREREQ_PM,
      );
      exit 1; # not reached
   }
}

require ExtUtils::Depends;
require ExtUtils::CFeatureTest;

my $dep= ExtUtils::Depends->new('Crypt::MultiKey', 'Crypt::SecretBuffer');
my $test= ExtUtils::CFeatureTest->new;

$test->header('stdbool.h');
$test->feature(HAVE_BOOL      => 'bool x= true; return x == false;');
$test->header('stdint.h');
$test->feature(HAVE_ENDIAN_H => 'uint32_t le= htole32(0x12345678); return le > 0? 0 : 1;',
               { h => 'endian.h' });

$test->require_feature(HAVE_LIBSSL
               => 'unsigned char buf[1]; return RAND_bytes(buf, 1) == 1? 0 : 1;',
               { h => 'openssl/rand.h', pkg_config => ['libressl'] },
               { h => 'openssl/rand.h', pkg_config => ['openssl'] },
               { h => 'openssl/rand.h', -l => [ 'ssl', 'crypto' ] });

$test->write_config_header('CryptMultiKey_config.h');
$test->export_deps($dep);

$dep->install('cmk.h');
$dep->add_c('cmk.c');
$dep->add_xs('CryptMultiKey.xs');
$dep->add_pm(map { my $n= $_; $n =~ s/^lib/\$(INST_LIB)/; $_ => $n } <lib/*/*.pm>, <lib/*/*/*.pm>);
$dep->add_typemaps('typemap');
$dep->save_config('InstallFiles.pm');

my %other_vars;
$other_vars{DEFINE}= '-DTEST_CRYPT_MULTIKEY_EXHAUSTIVE=1'
  if $ENV{TEST_CRYPT_MULTIKEY_EXHAUSTIVE};
